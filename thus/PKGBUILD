# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>
# Contributor: Alex Filgueira <alexfilgueira@antergos.com>

_pkgname=thus
pkgname=thus
pkgver=0.8.9
pkgrel=2
pkgdesc='Graphical Installer for Manjaro Linux'
arch=(any)
license=(GPL)
url="https://github.com/manjaro/thus.git"
depends=(coreutils cryptsetup gptfdisk grep libtimezonemap hdparm hwinfo lvm2 mhwd
         pam_encfs py3parted python python-configobj python-gobject python-dbus
         rsync sudo systemd upower util-linux webkitgtk3 e2fsprogs keyboardctl
         dosfstools jfsutils ntfs-3g ntfsprogs reiserfsprogs xfsprogs
         ttf-aboriginal-sans ttf-indic-otf ttf-khmer ttf-lohit-fonts ttf-myanmar3
         ttf-thaana-fonts ttf-tlwg iso-codes)
install=thus.install
backup=(etc/thus.conf)
_git=Yes
_development=No

if [ "$_git" == "Yes" ]; then
   makedepends=(git)
   source=(git+https://github.com/netrunnerarch/thus.git)
   sha256sums=('SKIP')
else
   source=("$pkgname-$pkgver.tar.gz::https://github.com/manjaro/thus/archive/$pkgver.tar.gz")
   sha256sums=('a785465ab4217806216d95eca4b3380e6bcb137bdc833d1e876aeeaf0b9a92bf')
fi

prepare() {
	if [ "$_development" == "Yes" ]; then
		cd "$srcdir/$pkgname"
		git checkout development
	fi
}

package() {
	if [ -e "$srcdir/$pkgname-$pkgver" ]; then
		cd "$srcdir/$pkgname-$pkgver"
	fi
	if [ -e "$srcdir/$pkgname" ]; then
		cd "$srcdir/$pkgname"
	fi
	if [ -e "$srcdir/$pkgname.git" ]; then
		cd "$srcdir/$pkgname.git"
	fi

	install -d ${pkgdir}/usr/share/locale/
	install -d ${pkgdir}/usr/share/thus
	install -Dm755 "${_pkgname}.py" "$pkgdir/usr/share/thus/${_pkgname}.py"
	install -Dm755 "${_pkgname}" "$pkgdir/usr/bin/thus"
	install -D -m644 $pkgname.desktop ${pkgdir}/usr/share/applications/$pkgname.desktop

	for i in data scripts src ui; do
		cp -R ${i} "${pkgdir}/usr/share/thus/"
	done

	for i in etc; do
		cp -R ${i} "${pkgdir}/"
	done

	# copy en_GB as en
	cp po/en_GB.po po/en.po

  	for files in po/*; do
    	if [ -f "$files" ] && [ "$files" != 'po/thus.pot' ]; then
        	STRING_PO=`echo ${files#*/}`
        	STRING=`echo ${STRING_PO%.po}`
        	mkdir -p ${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES
        	msgfmt $files -o ${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES/thus.mo
        	echo "${STRING} installed..."
    	fi
	done
}

